:toc: left
:toclevels: 5
:sectnums:
:stem:
:source-highlighter: coderay

= AxonFrameworkのブートストラップ

このステップでは、プロジェクトでAxonFrameworkを使用するための依存関係を設定します。

=== レンタルモジュールでのAxon依存関係の宣言

./rental/build.gradle.kts
[source,kotlin]
--
plugins {
	id("org.springframework.boot") version "3.4.0"
	id("io.spring.dependency-management") version "1.1.6"
}

version = "0.0.1-SNAPSHOT"

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(21))
	}
}

val axonVersion by extra("4.9.4")

dependencyManagement {
	imports {
		mavenBom("org.axonframework:axon-bom:$axonVersion")
	}
}

dependencies {
	implementation(platform("org.axonframework:axon-bom:$axonVersion"))
	implementation("io.axoniq.console:console-framework-client-spring-boot-starter:1.7.3")
	runtimeOnly("org.springframework.boot:spring-boot-docker-compose")

	implementation("org.axonframework:axon-spring-boot-starter")
	implementation("io.axoniq.console:console-framework-client-spring-boot-starter")
	implementation("org.springframework.boot:spring-boot-starter-actuator")
	implementation("org.springframework.boot:spring-boot-starter-data-jpa")
	implementation("org.springframework.boot:spring-boot-starter-webflux")
	runtimeOnly("com.h2database:h2")
	testImplementation("org.junit.jupiter:junit-jupiter-api:5.11.3")
	testImplementation("org.axonframework:axon-test")
	testImplementation("org.springframework.boot:spring-boot-starter-test")
	api(project(":core-api"))
}

tasks.named<Test>("test") {
	useJUnitPlatform()
}
--

=== `core-api` モジュールにAxonを使用したメッセージモデリングのサポートを追加

`core-api` モジュールを使用して、レンタルアプリケーションのさまざまなモジュールと通信するメッセージを定義します。そのメッセージの一部には、AxonFrameworkによって提供されるいくつかのJavaアノテーションを使用する必要があります。したがって、プロジェクトにいくつかのAxonFramework依存関係を追加する必要があります。

`core-api` に次の依存関係を追加するだけです：

./core-api/build.gradle.kts
[source,kotlin]
----
plugins {
	id("org.springframework.boot") version "3.4.0" apply false
	id("io.spring.dependency-management") version "1.1.6"
}

version = "0.0.1-SNAPSHOT"

java {
	sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
	}
}

dependencies {
	testImplementation("org.junit.jupiter:junit-jupiter-api:5.11.3")
	compileOnly("org.springframework.boot:spring-boot-starter-data-jpa")
	implementation("org.axonframework:axon-modelling:4.9.4")
}

tasks.named<Test>("test") {
	useJUnitPlatform()
}

tasks.withType<JavaCompile> {
	options.compilerArgs.add("-parameters")
}
----

これで、自転車レンタルアプリケーションの最初の機能の実装を開始する準備が整いました。